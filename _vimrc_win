"===================================================================
" Core rules for vimrc under Ms-Windows systems
"
" File		: _vimrc_win
" Maintainer	: Luc Hermitte <EMAIL:hermitte {at} free {dot} fr>
" 		  <URL:http://code.google.com/p/lh-vim/>
" Last update	: $Date$ (08th May 2010)
" ======================================================================
" Font {{{
try
  " set gfn=Andale_Mono:h10
  " set gfn=Andale_Mono:h8
  set gfn=Lucida_Console:h8
catch /E596/
  call confirm("Can't load the font Lucida_Console", '&Ok', 1)
endtry
" Font }}}

" I don't have TCL installed, and as has('tcl') freeze my win32-vim + cygwin, I
" deactivate it with:
let g:vimembedscript = 1

" Selection -> clipboard
set guioptions+=a
set clipboard+=autoselect

" Why do we need to bother with '\' ?
set shellslash
" ======================================================================
" Running IE {{{
" Note: These mappings require the tool cygstart that works under Cygwin.
" Related information:
" http://vim.sourceforge.net/tips/tip.php?tip_id=628
" http://vim.sourceforge.net/tips/tip.php?tip_id=394

let s:browser = executable('cygstart') ? 'cygstart' : ''

function! s:Browser()
  return lh#option#get('browser', s:browser, '[bg]') . ' '
endfunction

function! s:RunBrowser(url)
  let url = escape(a:url, '%')
  if SystemDetected() == 'unix'
    silent exe ':! '.<sid>Browser().url
  else
    silent exe ':! start '.<sid>Browser().url
  endif
endfunction

nnoremap <silent> <C-L>e 
      \ :call <sid>RunBrowser(
      \    matchstr(escape(expand('<cWORD>'), '#'), 
      \    '\%(.\{-}\zs\%(news:\\|mailto:\\|ftp://\\|https\=://\)\)\=[^:;,<>]\+'
      \ ))<CR>

" Problem with urls ending in .html
" nnoremap <silent> <C-L>m 
      " \ :exe ':!start rundll32 url.dll,FileProtocolHandler '.
      " \    matchstr(expand('<cWORD>'), 
      " \    '\%(.\{-}\zs\%(news:\\|mailto:\\|ftp://\\|https\=://\)\)\=[^:;,<>]\+'
      " \ )<CR>

vnoremap <silent> <C-L>e 
      \ :call <sid>RunBrowser(lh#visual#selection())<cr>

" Running IE }}}
" ======================================================================
if !has('gui_running')
  " mappings for vim (not gvim)
  map Œ÷ <M-Insert>
  map! Œ÷ <M-Insert>

  map ŒŸ <M-del>
  map! ŒŸ <M-del>

  map Œb <C-F5>
  map! Œb <C-F5>

  map Œn <M-F7>
  map! Œn <M-F7>

endif
" ======================================================================
" vim600: set foldmethod=marker:
